apply plugin: 'com.android.application'
apply from: '../url.build.gradle'

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    defaultConfig {
        applicationId rootProject.ext.android.applicationId
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        //阿里路由框架配置
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
        manifestPlaceholders = [APP_NAME     : "房脉通",
                                JPUSH_PKGNAME: applicationId,
                                JPUSH_APPKEY : "05f17a90dcb5c8a75b2884e2", //JPush 上注册的包名对应的 Appkey.
                                JPUSH_CHANNEL: "developer-default",
                                BAIDU_MAP_KEY: "PA7crUrNaePoKptkWGbFxNM5iiE8zr8A",
                                qqappid      : '1107813498'
        ]
//        ndk {
//            //设置支持的SO库架构
//            abiFilters 'armeabi' //, 'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'
//        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            //def date = new Date().format("yyyyMMdd" , TimeZone.getTimeZone("Asia/Shanghai"))
            def date = new Date().format("yyyyMMdd", TimeZone.getTimeZone("GMT+08"))
            if (variant.buildType.name == 'debug') {
                outputFileName = "${date}-v${variant.versionName}-${variant.flavorName}-debug.apk"
            }
            if (variant.buildType.name == 'release') {
                outputFileName = "${date}-v${variant.versionName}-${variant.flavorName}-release.apk"
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    //宿主项目中判断组件是否需要独立运行
    if (isBuildModule.toBoolean()) {
        //组件独立运行时，宿主项目直接依赖基础库，避免编译错误
        implementation project(':library-base')
    } else {
        //启动模块
        implementation project(':module_launcher')
        //启动模块
        implementation project(':module_main')
        //首页模块
        implementation project(':module_home')
        //房源模块
        implementation project(':module_hr')
        //资讯模块
        implementation project(':module_information')
        //登录模块
        implementation project(':module_login')
        //消息模块
        implementation project(':module_message')
        //用户模块
        implementation project(':module_mine')
        //公共模块
        implementation project(':module_public')
        //朋友圈模块
        implementation project(':module_friend_circle')
    }
}
